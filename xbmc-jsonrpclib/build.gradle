import org.xbmc.android.jsonrpc.generator.GradleTask

buildscript {

	project.getTasks().create([name: "generator", type: GradleBuild]) {
		buildFile = '../generator/build.gradle'
		tasks = ['install']
	}.execute()

	repositories {
		mavenCentral()
		mavenLocal()
	}

	dependencies {
		classpath 'com.android.tools.build:gradle:1.0.0'
		classpath group: 'org.xbmc.android.jsonrpc.generator',
				name: 'Generator',
				version: '1.0-SNAPSHOT'
	}
}

apply plugin: "android-library"
apply plugin: "generator"

dependencies {
	compile 'org.codehaus.jackson:jackson-core-asl:1.8.11'
	compile 'org.codehaus.jackson:jackson-mapper-asl:1.8.11'
	compile 'com.koushikdutta.async:androidasync:1.2.4'
}

android {
	compileSdkVersion 19
	buildToolsVersion "21.1.2"

	defaultConfig {
		minSdkVersion 9
		targetSdkVersion 19
		versionCode 1
		versionName "1.0"
	}

	sourceSets {
		main.java.srcDirs = ['src/main/java', 'src-gen/main/java']
	}
}

generator {
	srcDir = file('../generator')
	outputDir = file('../xbmc-jsonrpclib/src-gen/main/java')
}

task generateLibrarySources(type: GradleTask) {
}

gradle.projectsEvaluated {
	preBuild.dependsOn(generateLibrarySources)
}

// disable lint for faster builds - enable and fix before releasing!
project.afterEvaluate {
	project.tasks.lint.enabled = false
}
